import java.nio.file.Paths
import groovy.json.JsonSlurper

buildscript {
  repositories {
    google()
    gradlePluginPortal()
  }

  def kotlin_version = rootProject.ext.has("kotlinVersion") ? rootProject.ext.get("kotlinVersion") : project.properties["OPSQLite_kotlinVersion"]

  dependencies {
    classpath("com.android.tools.build:gradle:7.3.1")
    // noinspection DifferentKotlinGradleVersion
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

def resolveBuildType() {
    Gradle gradle = getGradle()
    String tskReqStr = gradle.getStartParameter().getTaskRequests()['args'].toString()

    return tskReqStr.contains('Release') ? 'release' : 'debug'
}

def isNewArchitectureEnabled() {
  return project.hasProperty("newArchEnabled") && project.newArchEnabled == "true"
}

def useSQLCipher = false
def useLibsql = false
def useCRSQLite = false
def performanceMode = false
def sqliteFlags = ""
def enableFTS5 = false
def useSqliteVec = false
def enableRtree = false
def tokenizers = []

def isInsideNodeModules = rootDir.absolutePath.contains("node_modules")
def packageJson

if ( isInsideNodeModules ) {
  def packageJsonFile = new File("$rootDir/../../../package.json")
  packageJson = new JsonSlurper().parseText(packageJsonFile.text)
} else {
  def packageJsonFile = new File("$rootDir/../package.json")
  packageJson = new JsonSlurper().parseText(packageJsonFile.text)
}

def opsqliteConfig = packageJson["op-sqlite"]
if(opsqliteConfig) {
  useSQLCipher = opsqliteConfig["sqlcipher"]
  useCRSQLite = opsqliteConfig["crsqlite"]
  useSqliteVec = opsqliteConfig["sqliteVec"]
  performanceMode = opsqliteConfig["performanceMode"]
  sqliteFlags = opsqliteConfig["sqliteFlags"] ? opsqliteConfig["sqliteFlags"] : ""
  enableFTS5 = opsqliteConfig["fts5"]
  useLibsql = opsqliteConfig["libsql"]
  enableRtree = opsqliteConfig["rtree"]
  tokenizers = opsqliteConfig["tokenizers"] ? opsqliteConfig["tokenizers"] : []
}

if(useSQLCipher) {
  println "[OP-SQLITE] using SQLCipher ðŸ”’"
} else if(useLibsql) {
  println "[OP-SQLITE] using libsql ðŸ“¦"
} else {
  println "[OP-SQLITE] using Vanilla SQLite"
}

if(useCRSQLite) {
  println "[OP-SQLITE] using CR-SQLite ðŸ¤–"
}

if(performanceMode) {
  println "[OP-SQLITE] Performance mode enabled! ðŸš€"
}

if(enableFTS5) {
  println "[OP-SQLITE] FTS5 enabled! ðŸ”Ž"
}

if(enableRtree) {
  println "[OP-SQLITE] RTree enabled! ðŸŒ²"
}

if(useSqliteVec) {
    println "[OP-SQLITE] Sqlite Vec enabled! â†—ï¸"
}


if (!tokenizers.isEmpty()) {
  println "[OP-SQLITE] Tokenizers enabled! ðŸ§¾ Tokenizers: " + tokenizers
}

if (isNewArchitectureEnabled()) {
    apply plugin: "com.facebook.react"
}

apply plugin: 'com.android.library'
apply plugin: "kotlin-android"

def safeExtGet(prop, fallback) {
  rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

def getExtOrDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties["OPSQLite" + name]
}

android {
  
  compileSdkVersion safeExtGet("compileSdkVersion", 33)
  namespace "com.op.sqlite"
  
  // Used to override the NDK path/version on internal CI or by allowing
  // users to customize the NDK path/version from their root project (e.g. for M1 support)
  if (rootProject.hasProperty("ndkPath")) {
    ndkPath rootProject.ext.ndkPath
  }
  if (rootProject.hasProperty("ndkVersion")) {
    ndkVersion rootProject.ext.ndkVersion
  }

  buildFeatures {
    prefab true
    prefabPublishing true
  }

  defaultConfig {
    minSdkVersion safeExtGet("minSdkVersion", 24)
    targetSdkVersion safeExtGet('targetSdkVersion', 35)
    versionCode 1
    versionName "1.0"
    
    externalNativeBuild {
        cmake {
            if(useSQLCipher) {
              cFlags += "-DOP_SQLITE_USE_SQLCIPHER=1"
              cppFlags += "-DOP_SQLITE_USE_SQLCIPHER=1"
            }
            if(useLibsql) {
              cFlags += "-DOP_SQLITE_USE_LIBSQL=1"
              cppFlags += "-DOP_SQLITE_USE_LIBSQL=1"
            }
            if(useCRSQLite) {
              cFlags += "-DOP_SQLITE_USE_CRSQLITE=1"
              cppFlags += "-DOP_SQLITE_USE_CRSQLITE=1"
            }
            if(performanceMode) {
              cFlags += ["-DSQLITE_DQS=0", "-DSQLITE_THREADSAFE=1", "-DSQLITE_DEFAULT_MEMSTATUS=0", "-DSQLITE_DEFAULT_WAL_SYNCHRONOUS=1", "-DSQLITE_LIKE_DOESNT_MATCH_BLOBS=1", "-DSQLITE_MAX_EXPR_DEPTH=0", "-DSQLITE_OMIT_DEPRECATED=1", "-DSQLITE_OMIT_PROGRESS_CALLBACK=1", "-DSQLITE_OMIT_SHARED_CACHE=1", "-DSQLITE_USE_ALLOCA=1"]
            }
            if(enableFTS5) {
              cFlags += ["-DSQLITE_ENABLE_FTS5=1"]
            }
            if(enableRtree) {
              cFlags += ["-DSQLITE_ENABLE_RTREE=1"]
            }
            if(useSqliteVec) {
              cFlags += "-DOP_SQLITE_USE_SQLITE_VEC=1"
              cppFlags += "-DOP_SQLITE_USE_SQLITE_VEC=1"
            }

            // This are zeroes because they will be passed as C flags, so they become falsy
            def sourceFiles = 0
            // def tokenizerInitStrings = 0
            def tokenizersHeaderPath = 0
            if (!tokenizers.isEmpty()) {
              def sourceDir = isInsideNodeModules ? file("$rootDir/../../../c_sources") : file("$rootDir/../c_sources")
              def destDir = file("$buildscript.sourceFile.parentFile/c_sources")
              copy {
                from sourceDir
                into destDir
                include "**/*.cpp", "**/*.h"
              }
              sourceFiles = fileTree(dir: destDir, include: ["**/*.cpp", "**/*.h"]).files.join(";")
              tokenizersHeaderPath = "../c_sources/tokenizers.h" 
            }

            cppFlags "-O2 -frtti -fexceptions -Wall -fstack-protector-all"
            abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
            arguments "-DANDROID_STL=c++_shared",
              "-DSQLITE_FLAGS='$sqliteFlags'",
              "-DUSE_SQLCIPHER=${useSQLCipher ? 1 : 0}",
              "-DUSE_CRSQLITE=${useCRSQLite ? 1 : 0}",
              "-DUSE_LIBSQL=${useLibsql ? 1 : 0}",
              "-DUSE_SQLITE_VEC=${useSqliteVec ? 1 : 0}",
              "-DUSER_DEFINED_SOURCE_FILES=${sourceFiles}",
              "-DUSER_DEFINED_TOKENIZERS_HEADER_PATH='${tokenizersHeaderPath}'"
        }
    }

    packagingOptions {
      doNotStrip resolveBuildType() == 'debug' ? "**/**/*.so" : ''
      excludes = [
              "META-INF",
              "META-INF/**",
              "**/libjsi.so",
              "**/libreact_nativemodule_core.so",
              "**/libturbomodulejsijni.so",
              "**/libc++_shared.so",
              "**/libfbjni.so",
              "**/libreactnative.so",
      ]
    }
    
  }
  
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  externalNativeBuild {
    cmake {
      path "CMakeLists.txt"
    }
  }

  sourceSets.main {
    java {
      // TODO removed the codegen config to allow the package to be built under bridgeless
      // Once there is a stable API for C++ Turbo Modules, maybe this can be enabled again
      // if (!isNewArchitectureEnabled()) {
        srcDirs += 'src/paper/java'
      // }
    }
  }
}

repositories {
  mavenCentral()
  google()
}

def kotlin_version = getExtOrDefault("kotlinVersion")
dependencies {
  implementation 'com.facebook.react:react-native'
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  if (useSQLCipher) {
    implementation('com.android.ndk.thirdparty:openssl:1.1.1q-beta-1')
  }
}

// Resolves "LOCAL_SRC_FILES points to a missing file, Check that libfb.so exists or that its path is correct".
tasks.whenTaskAdded { task ->
  if (task.name.contains("configureCMakeDebug")) {
    rootProject.getTasksByName("packageReactNdkDebugLibs", true).forEach {
      task.dependsOn(it)
    }
  }
  // We want to add a dependency for both configureCMakeRelease and configureCMakeRelWithDebInfo
  if (task.name.contains("configureCMakeRel")) {
    rootProject.getTasksByName("packageReactNdkReleaseLibs", true).forEach {
      task.dependsOn(it)
    }
  }
}

if (isNewArchitectureEnabled()) {
  react {
    jsRootDir = file("../src/")
    libraryName = "opsqlite"
    codegenJavaPackageName = "com.op.sqlite.example"
  }
}
